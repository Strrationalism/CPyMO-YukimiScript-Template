
- operation ensure-build-dir %build-dir%
for %i% in [
    %build-dir%
    "%build-dir%/bg"
    "%build-dir%/bgm"
    "%build-dir%/chara"
    "%build-dir%/se"
    "%build-dir%/video"
    "%build-dir%/system"
    "%build-dir%/voice"
    "%build-dir%/script"
] {
    ensure-dir %i%
}

- operation clean-build-dir %build-dir%
if (dir-exists %build-dir%) {
    for %i% in (all-files %build-dir%) {
        delete-file "%build-dir%/%i%"
    }

    for %i% in (all-dirs %build-dir%) {
        delete-dir "%build-dir%/%i%"
    }

    delete-dir %build-dir%
}

- task Pack-Files %dir-to-pack% %output-pak-file%
set %files-to-pack% (get-files %dir-to-pack%)
if (not (is-empty %files-to-pack%)) {
    output %output-pak-file%
    for %i% in %files-to-pack% {
        input "%dir-to-pack%/%i%"
    }
}

- operation Pack-Files %dir-to-pack% %output-pak-file%
set %list-file% (change-extension ".txt" %output-pak-file%)
write-text %input% %list-file%
cpymo-tool pack %output% --file-list %list-file%
delete-file %list-file%

- task Copy-File %src-file% %dest-file%
input %src-file%
output %dest-file%

- operation Copy-File %src-file% %dest-file%
copy-file (fst %input%) (fst %output%)

- task Copy-Dir %src% %dst%
ensure-dir %src%
ensure-dir %dst%
for %i% in (get-files %src%) { 
    Copy-File "%src%/%i%" "%dst%/%i%"
}

- task Compile-Script %src-file% %dst-file% %lib-files% %lib-dir% %libpymo%
input %src-file%
input %libpymo%
for %i% in %lib-files% {
    input %i%
}
output %dst-file%

- operation Compile-Script %src-file% %dst-file% %lib-files% %lib-dir% %libpymo%
ykmc %src-file% --target-pymo %dst-file% --lib %lib-dir%

- task Compile-Scripts %src-dir% %dst-dir% %lib-dir% %libpymo%
set %lib-files% (get-files %lib-dir%)
set %lib-paths% []
for %lib-file% in %lib-files% {
    set %lib-paths% (add %lib-paths% ["%lib-dir%/%lib-file%"])
}

set %src-files% (get-files %src-dir%)
for %src-file% in %src-files% {
    set %dst-filename% (change-extension ".txt" %src-file%)
    Compile-Script "%src-dir%/%src-file%" "%dst-dir%/%dst-filename%" %lib-paths% %lib-dir% %libpymo%
}

- task Install-libpymo %target-dir%
ensure-dir %target-dir%
output "%target-dir%/libpymo.ykm"
set %url% "https://raw.githubusercontent.com/Strrationalism/CPyMO/main/libpymo.ykm"

- operation Install-libpymo %target-dir% for windows
PowerShell curl %url% -OutFile %output%

- operation Install-libpymo %target-dir% for { macos, linux }
curl %url% --output %output%

- task Install-libpymo-IfNotInstalled %target-dir%
if (not (file-exists "%target-dir%/libpymo.ykm")) {
    Install-libpymo %target-dir%
}
